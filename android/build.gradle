// Plugins
apply plugin: 'com.android.application'

// Get the git hash to use in the mobile app
def gitHash() {
    try {
        // determine git-describe output
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'log', '--pretty=format:\'%h\'', '-1'
            standardOutput = stdout
        }
        return stdout.toString().trim().replaceAll("'", "")
    } catch (e) {
        return "unknown"
    }
}

// Android configuration
android {
    compileSdkVersion 21
    buildToolsVersion '21.1.2'

    sourceSets {
        main {
            java.srcDirs 'src/main/java'
            res.srcDirs 'src/main/resources'
            manifest.srcFile 'src/main/AndroidManifest.xml'
        }
    }

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 20
        versionCode 1
        versionName "1.0.0-" + gitHash()
    }

    lintOptions {
        abortOnError false
    }
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.1'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.android.support:appcompat-v7:22.2.0'
    provided 'org.projectlombok:lombok:1.14.4'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.google.guava:guava:17.0'
    compile 'commons-io:commons-io:2.4'
}